/*******************************************************************************
* Instrument:  N_diffractometer
*
* %I
* Written by: Peter Willendrup <pkwi@fysik.dtu.dk>
* Date: September 2017
* Origin: DTU Physics
* %INSTRUMENT_SITE: DTU_10200
*
* Simple neutron powder diffraction instrument with a source, a sample and some monitors. 
*
* %D
* Simple neutron powder diffraction instrument with a source, a sample and some monitors.
*
* The included monitors are:
* a) A big, rectangular "screen" behind the sample for visualising Debye-Scherrrer rings
* b) A 2-dimensional I vs. angle x height, cylindrical "banana" monitor
* c) A 1-dimensional I vs. angle cylindrical "banana" monitor 
* d) A 2-dimensional I vs. angle and ToF monitor for the "TOF" mode
* 
* Has two main modes of operation:
* 1) TOF=0 "reactor" mode
* 2) TOF=1 Time-of-Flight mode
*
* Idealisations:
* a) No "direct beam" is tunneled through the sample.
*
* Example: <parameters=values>
*
* %P
* lambda:     [AA]      Central wavelength produced by the source
* dlambda:    [AA]      Width +/- of wavelength band around central wavelength
* powderfile: [string]  "Structure" datafile for studied material
* d_phi:      [deg]     When d_phi == 180 deg, the sample illuminates "all angles around the scattering plane". When d_phi ~ 20 deg, scattering will be restricted directionally to "hit" the banana-shaped detectors
* TOF:        [1]       "Reactor" or "Time-of-Flight" mode
* NU:         [Hz]      Frequency of the chopper.
* THETA:      [deg]     Angular opening of the chopper window.
* ANGLE:      [deg]     Grazing angle of the monochromator crystal. 
*
* %L
* <reference/HTML link>
*
* %E
*******************************************************************************/
DEFINE INSTRUMENT N_diffractometer(lambda=10,dlambda=9.5,string powderfile="Na2Ca3Al2F14.laz", d_phi=180, TOF=0, NU=100, THETA=10, ANGLE=0)

DECLARE
%{
%}

INITIALIZE
%{
%}

TRACE

COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

COMPONENT Source = Source_Maxwell_3(
    yheight=0.1, 
    xwidth=0.1, 
    Lmin=0.05, 
    Lmax=20, 
    dist=20, 
    focus_xw=0.01, 
    focus_yh=0.07, 
    T1=150.42, 
    T2=38.74, 
    T3=14.84, 
    I1=3.67E11, 
    I2=3.64E11, 
    I3=0.95E11, 
    lambda0=lambda, 
    dlambda=dlambda)
AT (0, 0, 0) RELATIVE PREVIOUS
EXTEND %{
  if (!INSTRUMENT_GETPAR(TOF)) t=rand01();
%}

COMPONENT Lambda_source = L_monitor(
    nL=201,
    filename="Lambda_source.dat",
    Lmin=0.05,
    Lmax=20,
    xwidth=0.101,
    yheight=0.101, restore_neutron=1)
AT (0, 0, 9.8) RELATIVE Source

COMPONENT mono_arm = Arm()
WHEN(TOF==0)AT(0,0,10) RELATIVE Source

COMPONENT mx = Monochromator_flat(r0=1)
WHEN(TOF==0 && ANGLE!=0) AT(0,0,0) RELATIVE PREVIOUS
ROTATED (0,ANGLE,0) RELATIVE PREVIOUS

COMPONENT mono_arm_x = Arm()
WHEN(TOF==0) AT(0,0,0) RELATIVE PREVIOUS
ROTATED (0,ANGLE,0) RELATIVE PREVIOUS


COMPONENT Chopper = DiskChopper(isfirst=1,radius=0.3, yheight=0.1, theta_0=THETA, nu=NU)
WHEN (TOF==1) AT (0,0,19-10) RELATIVE mono_arm_x

COMPONENT Lambda_mono = COPY(Lambda_source)(filename="Lambda_mono.dat")
AT(0,0,0.01) RELATIVE PREVIOUS

SPLIT 10 COMPONENT Sample = PowderN(reflections=powderfile, radius=0.005, yheight=0.07, d_phi=d_phi, p_inc=0.01, p_transmit=0)
  AT (0,0,10) RELATIVE mono_arm_x
EXTEND %{
  if(!SCATTERED) ABSORB;
%}

  COMPONENT DebyeS_rectangular = Monitor_nD(xwidth=2, yheight=2 ,options="x bins=401, y bins=401", restore_neutron=1, filename="DebyeS_rectangular")
  AT (0,0,1) RELATIVE Sample

  COMPONENT Powder_2D_banana = Monitor_nD(radius=1, yheight=0.2,options="banana, theta limits=[10,170] bins=320, y limits=[-0.1,0.1] bins=81", restore_neutron=1, filename="Powder_2D")
  AT (0,0,0) RELATIVE Sample

  COMPONENT Powder_1D_banana = Monitor_nD(radius=1, yheight=0.2,options="banana, theta limits=[10,170] bins=320, ", restore_neutron=1, filename="Powder_1D")
  AT (0,0,0) RELATIVE Sample

  COMPONENT TOF_2D_banana  = Monitor_nD(radius=1, yheight=0.2,options="banana, theta limits=[10,170] bins=320, auto tof bins=2000", restore_neutron=1, filename="TOF_2D")
  AT (0,0,0) RELATIVE Sample
  

FINALLY
%{
%}

END
